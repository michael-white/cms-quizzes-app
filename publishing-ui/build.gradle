apply plugin: 'war'
apply plugin: 'jetty'


ext {
    jettyVersion = '9.2.9.v20150224'
}


configurations {
    executableWar
}


dependencies {

    //Jetty
    compile "org.eclipse.jetty.aggregate:jetty-all:$jettyVersion"

    compile project(':articles-app')
    compile project(':slideshows-app')
    compile project(':cloudinary-app')
    compile project(':publishing-commons')
    compile project(':healthguides-app')

    executableWar("org.eclipse.jetty.aggregate:jetty-all:$jettyVersion",
            "com.sharecare.services:embedded-jetty:1.0")

}

sourceSets.all {
    compileClasspath += configurations.executableWar
}


war {
    from {
        configurations.executableWar.collect {
            return it.isDirectory() ? it : zipTree(it)
        }
    }
    from "$buildDir/classes/main"
    exclude('**/META-INF/*.SF', '**/META-INF/*.RSA', '**/META-INF/*.DSA')
    manifest {
        attributes 'Main-Class': 'com.sharecare.cms.boot.Server'
    }
}

apply from: "gradle/artifactPublishing.gradle"

